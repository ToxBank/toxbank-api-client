#Investigation service query
#Retrieves investigation URI given characteristic value  (e.g. HepaRG)
#
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX isa:   <http://onto.toxbank.net/isa/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX tb:    <http://onto.toxbank.net/api/>

SELECT DISTINCT ?investigation ?propname ?ontoURI WHERE {
 ?investigation rdf:type isa:Investigation.
 ?investigation isa:hasStudy ?study.
 ?investigation tb:isPublished "true"^^<http://www.w3.org/2001/XMLSchema#boolean>.
 ?investigation tb:isSummarySearchable "true"^^<http://www.w3.org/2001/XMLSchema#boolean>.
 ?node isa:hasStudy ?study.
 ?node rdf:type isa:MaterialNode.
 ?node isa:hasMaterial ?sample.
 ?sample isa:hasCharacteristicValue ?propValue.
 OPTIONAL {?propValue isa:hasOntologyTerm ?ontoURI.} 
 ?propValue isa:hasValue  "%{value}".
 ?propValue isa:hasProperty ?property. 
 ?property dcterms:title ?propname.
} ORDER BY ?investigation
