# @markup ruby
# @title
# get investigation array data files
PREFIX isa:<http://onto.toxbank.net/isa/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?assay ?assayFile ?endpoint ?endpointLabel ?platform ?technology ?techLabel ?file ?term ?downloaduri WHERE {
  <%{investigation_uri}> isa:hasStudy ?study.
  ?study rdf:type isa:Study.
  ?datanode isa:hasStudy ?study.
  ?datanode rdf:type isa:DataNode.
  ?datanode isa:hasData ?data.
  OPTIONAL {?datanode isa:hasDownload ?downloaduri.}
  ?data rdf:type isa:Data.
  ?data isa:hasOntologyTerm ?term.
  ?data rdfs:seeAlso ?file.
  OPTIONAL {
    ?mp isa:hasOutputNode ?datanode.
    ?mp isa:hasInputNode ?materialnode.
    ?materialnode isa:hasMaterial ?material.
    ?assay isa:uses ?material.
    ?assay isa:hasEndpoint ?endpoint.
    ?assay rdfs:seeAlso ?assayFile.
    OPTIONAL {?endpoint rdfs:label ?endpointLabel.}
    OPTIONAL {?assay isa:usesPlatform ?platform.}
    ?assay isa:usesTechnology ?technology.
    OPTIONAL {?technology rdfs:label ?techLabel.}
  }
} ORDER BY ?assay ?endpoint ?technology
