#Investigation service query
#Retrieves protocol URI containing any of the factor value URI (e.g. two compound URIs)
#

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix isa:   <http://onto.toxbank.net/isa/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix tb:    <http://onto.toxbank.net/api/> 

SELECT distinct ?protocol ?label ?factorname ?value WHERE {
 ?inv rdf:type isa:Investigation.
 ?inv isa:hasStudy ?study.
 ?study isa:hasProtocol ?protocol.
 OPTIONAL {
 ?protocol rdfs:label ?label.
 }
 ?node isa:hasStudy ?study.
 ?node rdf:type isa:MaterialNode.
 ?node isa:hasMaterial ?sample.
 ?sample isa:hasFactorValue ?factorValue.
 {
  { ?factorValue isa:hasOntologyTerm  <http://purl.obolibrary.org/chebi/CHEBI:39867> } 
 UNION
  {?factorValue isa:hasOntologyTerm  
<http://toxbanktest2.toxbank.net:8080/ambit2/compound/GJPICJJJRGTNOD-UHFFFAOYSA-N>
  }
 }.
 ?factorValue isa:hasValue  ?value.
 ?factorValue isa:hasFactor ?factor. 
 ?factor dcterms:title ?factorname.
} order by ?factorValue 