#Investigation service query
#Retrieves all available factor combinations given an investigation URI
#
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX isa:   <http://onto.toxbank.net/isa/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?sample ?factorname ?value ?ontouri ?unitOnto ?unit ?unitID WHERE {
 <%{investigation_uri}> isa:hasStudy ?study.
 ?node isa:hasStudy ?study.
 ?node rdf:type isa:MaterialNode.
 ?node isa:hasMaterial ?sample.
 ?sample isa:hasFactorValue ?factorValue.
 OPTIONAL {?factorValue isa:hasOntologyTerm  ?ontouri }.
 ?factorValue isa:hasValue  ?value.
 OPTIONAL {
  ?factorValue isa:hasUnit ?unitOnto. 
  ?unitOnto rdfs:label ?unit.
  ?unitOnto isa:hasAccessionID ?unitID.
 }
 ?factorValue isa:hasFactor ?factor. 
 ?factor dcterms:title ?factorname.
} order by ?sample ?factorname
